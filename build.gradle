plugins {
    id "com.diffplug.spotless" version "${spotlessVersion}" apply false
    id "com.google.protobuf" version "+" apply false
    id "io.github.danielliu1123.deployer" version "0.3.0"
}

deploy {
    // dirs to upload, they will all be packaged into one bundle
    dirs = subprojects.collect { e -> e.layout.buildDirectory.dir("repo").get().getAsFile() }
    username = System.getenv("MAVENCENTRAL_USERNAME")
    password = System.getenv("MAVENCENTRAL_PASSWORD")
    publishingType = io.github.danielliu1123.deployer.PublishingType.AUTOMATIC
}

subprojects {
    apply plugin: "java"
    apply plugin: "java-library"

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += ["-parameters"]
        options.encoding = "UTF-8"
    }

    javadoc {
        // Suppress warnings about missing Javadoc comments
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    configurations {
        optional
        compileOnly.extendsFrom optional
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        testCompileOnly("org.projectlombok:lombok:+")
        testAnnotationProcessor("org.projectlombok:lombok:+")

        testImplementation(platform("org.junit:junit-bom:+"))
        // https://docs.gradle.org/current/userguide/upgrading_version_8.html#test_framework_implementation_dependencies
        testRuntimeOnly("org.junit.platform:junit-platform-launcher")
    }

    test {
        useJUnitPlatform()
    }

    apply plugin: "com.diffplug.spotless"
    spotless {
        encoding "UTF-8"
        java {
            toggleOffOn()
            removeUnusedImports()
            trimTrailingWhitespace()
            endWithNewline()
            palantirJavaFormat()

            targetExclude "**/generated/**"

            custom("Refuse wildcard imports", {
                if (it =~ /\nimport .*\*;/) {
                    throw new IllegalStateException("Do not use wildcard imports, 'spotlessApply' cannot resolve this issue, please fix it manually.")
                }
            } as Closure<String>)
        }
    }
}
